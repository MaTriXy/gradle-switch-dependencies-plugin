/*
 * Copyright 2015 Francesco Pontillo
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.2"
    }
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: "groovy"

dependencies {
    compile gradleApi()
    compile localGroovy()
}

// read the plugin IDs
def pluginIds = project
        .fileTree(dir: "src/main/resources/META-INF/gradle-plugins", include: "*.properties")
        .collect { it.name[0..(it.name.lastIndexOf(".") - 1)] }

// read tags array from properties
def propTags = project.properties
        .findAll { it.key.startsWith("tags.") }
        .collect { it.value}

// load gradle publishing key and secret from gradle.publish.properties
// (see https://issues.gradle.org/browse/GRADLE-3281)
Properties props = new Properties()
new File("gradle.publish.properties").withInputStream { props.load(it) }
System.setProperty('gradle.publish.key', props.getProperty("key") ?: '')
System.setProperty('gradle.publish.secret', props.getProperty("secret") ?: '')

pluginBundle {
    vcsUrl = project.vcs
    website = project.vcs

    plugins {
        switchDependenciesPlugin {
            id = pluginIds[0]
            displayName = project.display
            description = project.description
            tags = propTags
        }
    }
}